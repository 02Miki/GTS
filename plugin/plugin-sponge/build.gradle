buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "snapshot_20180324"
    useDepAts = true
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name 'sponge-repo'
        url 'https://repo.spongepowered.org/maven'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven {
        name = "Github Packages - Protocol Control"
        url = "https://maven.pkg.github.com/ichorpowered/protocolcontrol"
        credentials {
            username = "NickImpact"
            password = project.properties['github_personal_access_token']
        }
    }
}

shadowJar {
    archiveName = "GTS-Sponge-1.12.2-${project.version}.jar"

    exclude 'protocolcontrol-*.jar'

    relocate ('net.kyori', 'me.nickimpact.gts.relocations.net.kyori')
    relocate ('com.github.benmanes', 'me.nickimpact.gts.relocations.com.github.benmanes')
    relocate ('com.google.errorprone', 'me.nickimpact.gts.relocations.com.google.errorprone')
    relocate ('org.checkerframework', 'me.nickimpact.gts.relocations.org.checkerframework')
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

dependencies {
    compile project(':api')
    compile project(':common')
    compile project(':sponge')
    shadow 'com.nickimpact.impactor:sponge:3.0.0-SNAPSHOT'
    shadow 'org.spongepowered:spongeapi:7.3.0-20200614.093206-8'
    shadow "com.ichorpowered:protocolcontrol:0.0.1"
    //shadow "co.aikar:acf-sponge:0.5.0-SNAPSHOT"
    compile('net.kyori:text-api:3.0.3') {
        exclude(module: 'checker-qual')
    }
    compile('net.kyori:text-serializer-legacy:3.0.3') {
        exclude(module: 'text-api')
    }
    compile('net.kyori:text-serializer-gson:3.0.3') {
        exclude(module: 'text-api')
        exclude(module: 'gson')
    }
    compile('net.kyori:text-adapter-spongeapi:3.0.3') {
        exclude(module: 'text-api')
        exclude(module: 'gson')
    }
}

sourceMainJava.finalizedBy(blossomSourceReplacementJava)
blossom {
    blossomSourceReplacementJava.output = file("$buildDir/sources/main/java")
    replaceToken '@version@', version
}

build.dependsOn reobfShadowJar
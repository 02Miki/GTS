buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "snapshot_20180324"
    useDepAts = true
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven {
        name 'sponge-repo'
        url 'https://repo.spongepowered.org/maven'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven {
        name = "Github Packages - Protocol Control"
        url = "https://maven.pkg.github.com/ichorpowered/protocolcontrol"
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        name = "CodeMC"
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

shadowJar {
    archiveName = "GTS-Sponge-${project.version}.jar"

    exclude 'protocolcontrol-*.jar'
    exclude 'dummyThing'

    relocate ('net.kyori.adventure', 'net.impactdev.impactor.relocations.adventure')
    relocate ('net.kyori.examination', 'net.impactdev.impactor.relocations.examination')
    relocate ('com.github.benmanes.caffeine', 'net.impactdev.impactor.relocations.caffeine')
    relocate ('io.github.classgraph', 'net.impactdev.impactor.relocations.classgraph')
    relocate ('co.aikar.commands', 'net.impactdev.impactor.relocations.commands')
    relocate ('org.mariuszgromada.math.mxparser', 'net.impactdev.impactor.relocations.mxparser')
    relocate ('redis.clients.jedis', 'net.impactdev.impactor.relocations.jedis')
    relocate ('org.apache.commons.pool2', 'net.impactdev.impactor.relocations.commonspool2')
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

dependencies {
    compile project(':api')
    compile project(':common')
    compile project(':sponge-common')
    shadow 'net.impactdev.impactor:sponge:3.1.1-SNAPSHOT'
    shadow 'org.spongepowered:spongeapi:7.3.0'
    shadow 'net.kyori:adventure-api:4.3.0'
    shadow 'net.kyori:adventure-text-serializer-legacy:4.3.0'
}

sourceMainJava.finalizedBy(blossomSourceReplacementJava)
blossom {
    blossomSourceReplacementJava.output = file("$buildDir/sources/main/java")
    replaceToken '@version@', version
    replaceToken '@git_commit@', gitHash.commit
    replaceToken '@description@', 'A player controlled market system'
}

blossomSourceReplacementJava.dependsOn(gitHash)
jar.dependsOn(gitHash)
build.dependsOn reobfShadowJar
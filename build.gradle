plugins {
    id 'com.qixalite.spongestart' version '1.6.2'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group = 'me.nickimpact.gts'
version = '4.2.0'

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    ext {
        sponge = "7.1.5"
        spigot = "1.12.2"
    }

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }

    jar {
        exclude 'Impactor*.jar'
    }

    shadowJar {
        exclude 'Impactor*.jar'
    }

    dependencies {
        compile fileTree(dir: '../../Impactor/build/allJars/', include: '*.jar')
        shadow "org.projectlombok:lombok:1.18.4"
        shadow 'com.google.guava:guava:27.1-jre'
        shadow 'com.google.code.gson:gson:2.8.5'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

task copyJars(type: Copy) {
    dependsOn subprojects.build
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/allJars"
    exclude '*-all.jar'
    exclude '*-sources.jar'
    exclude 'api*.jar'
    exclude 'common*.jar'
    exclude 'spigot*.jar'
    exclude 'sponge*.jar'
    exclude 'plugin*.jar'
    exclude 'reforged*.jar'
}

build.dependsOn shadowJar
build.dependsOn copyJars